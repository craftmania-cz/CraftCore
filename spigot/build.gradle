apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'


repositories {
    flatDir {dirs 'lib'}
    mavenCentral();
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "http://repo.dmulloy2.net/nexus/repository/public/" }
    maven { url "http://maven.sk89q.com/repo/" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "https://repo.destroystokyo.com/repository/maven-public//" }
    maven { url "http://ci.emc.gs/nexus/content/groups/aikar/" }
    maven { url "http://repo.md-5.net/content/repositories/snapshots/" }
    maven { url "https://packages.craftmania.cz/service/rest/repository/browse/craftmania/" }
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    maven { url 'https://jitpack.io' }
}

dependencies {

    // Core modules
    compile project(path: ':main')

    // Plugin dependency
    compile 'org.jetbrains:annotations:16.0.3'
    compile 'com.github.cryptomorin:XSeries:7.6.0'
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.5.0";
    compileOnly ('org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5'
    }
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.1-SNAPSHOT'

    // Spigot dependency
    compileOnly name: 'spigot-1.16.4'
}

def exportedProjects= [
        ":spigot",
        ":main"
]

clean { delete "build" }

compileJava {
    options.encoding = "UTF-8"
}

processResources {
    from('src/main/resources') {
        include 'plugin.yml'
        expand(
                version: project.parent.version
        )
    }
}

task alljavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
    options.setWindowTitle("CraftCore (" + project.parent.version + ")")
    options.setDocTitle("CraftCore (" + project.parent.version + ")")
}

shadowJar {
    dependencies {
        include(dependency(':main'))

        // Include chybějící deps pro 1.14.4+
        include(dependency('org.apache.commons:commons-lang3:3.8'))
        include(dependency('commons-io:commons-io:2.4'))
        include(dependency('com.github.cryptomorin:XSeries:7.6.0'))
        relocate 'com.cryptomorin.xseries', "cz.craftmania.craftcore.spigot.xseries"
    }
    archiveName = "${parent.name}-${parent.version}.jar"
    destinationDir = file '../target'
}

build.dependsOn(shadowJar)
